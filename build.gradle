buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }

        repositories {
            maven {
                url "https://plugins.gradle.org/m2/"

            }
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}
plugins {
    id 'java'
    id "com.github.breadmoirai.github-release" version "2.2.12"
    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}
apply plugin: 'com.github.johnrengelman.shadow'

version = "2.1.2"
group = "de.erdbeerbaerlp.dcintegration"
archivesBaseName = "dcintegration-spigot"


sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}
//ShadowJar
configurations {
    embed
    compile.extendsFrom(embed)
}
shadowJar {
    classifier = '1.16'
    configurations = [project.configurations.embed]
}
artifacts {
    archives tasks.shadowJar
}

//Generates plugin.yml
bukkit {
    name = "DiscordIntegration"
    main = "de.erdbeerbaerlp.dcintegration.spigot.DiscordIntegration"
    author = "ErdbeerbaerLP"
    apiVersion = "1.16"
    version = project.version
    website = "https://spigotmc.org/resources/discordintegration.87196/"
    commands {
        discord {
            aliases = ["dc"]
            permission = "dcintegration.discord"
        }
    }
    permissions {
        'dcintegration.discord' {
            description = "Gives access to the basic Discord Integration commands"
            setDefault('TRUE')
        }
        'dcintegration.admin' {
            description = "Gives access to the Discord Integration admin subcommands (like reload)"
            setDefault('OP')
        }
    }
    softDepend = ["Votifier", "dynmap", "floodgate-bukkit"]
}


repositories{
    jcenter()
    maven {
        name = 'sonatype-oss'
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url 'https://jitpack.io' }
    maven { url "https://nexus.vankka.dev/repository/maven-public/" }
    maven { url "https://repository.dev.gotan.io/repository/gotan.os/" }
    maven { url "https://repo.opencollab.dev/maven-snapshots" }
    maven { url "https://repo.opencollab.dev/maven-releases" }
    maven{
        name = 'howaner'
        url = "http://repo.howaner.de/"
    }
}
dependencies{

    // =========  Common  =====================
    embed 'de.erdbeerbaerlp:DiscordIntegration-Core:f8762f79a7'


    // ============= Spigot =============
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'


    //Votifier
    compileOnly('com.vexsoftware:votifier:1.9'){
        transitive = false
    }

    //Dynmap-Bukkit for workaround
    compileOnly('com.github.webbukkit.dynmap:dynmap-api:3.1-beta-7'){
        transitive = false
    }

    //Floodgate
    compileOnly('org.geysermc:floodgate-bukkit:1.0-SNAPSHOT')

    //Dynmap-API
    compileOnly('com.github.webbukkit:DynmapCoreAPI:2.5')
}
//Manifest attributes
jar {
    classifier = "1.16"
}

githubRelease {
    token  getProperty("github.token") // This is your personal access token with Repo permissions
    // You get this from your user settings > developer settings > Personal Access Tokens
    owner "erdbeerbaerlp" // default is the last part of your group. Eg group: "com.github.breadmoirai" => owner: "breadmoirai"
    repo "DiscordIntegration-Spigot" // by default this is set to your project name
    tagName "v${project.version}" // by default this is set to "v${project.version}"
    targetCommitish "master" // by default this is set to "master"
    releaseName "v${project.version}" // Release title, by default this is the same as the tagName
    prerelease false // by default this is false
    releaseAssets shadowJar // this points to which files you want to upload as assets with your release

    dryRun false // by default false; you can use this to see what actions would be taken without making a release
    apiEndpoint "https://api.github.com" // should only change for github enterprise users
}