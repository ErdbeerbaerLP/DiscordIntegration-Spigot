buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        repositories {
            maven {
                url "https://plugins.gradle.org/m2/"

            }
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
        classpath 'com.github.MMD-Forks:CurseGradle:693e9a980c' //Modified CurseGradle to work with dev.bukkit.org
    }
}
plugins {
    id 'java'
    id "com.github.breadmoirai.github-release" version "2.2.12"
    id 'net.minecrell.plugin-yml.bukkit' version '0.4.0'
}
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: 'com.github.johnrengelman.shadow'

version = "2.3.4"
group = "de.erdbeerbaerlp.dcintegration"
archivesBaseName = "dcintegration-spigot"


sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}
//ShadowJar
configurations {
    embed
    compileOnly.extendsFrom(embed)
}
shadowJar {
    classifier = '1.17'
    configurations = [project.configurations.embed]
}
artifacts {
    archives tasks.shadowJar
}

//Generates plugin.yml
bukkit {
    name = "DiscordIntegration"
    main = "de.erdbeerbaerlp.dcintegration.spigot.DiscordIntegration"
    author = "ErdbeerbaerLP"
    apiVersion = "1.13"
    version = project.version
    website = "https://spigotmc.org/resources/discordintegration.87196/"
    commands {
        discord {
            aliases = ["dc"]
            permission = "dcintegration.discord"
        }
    }
    permissions {
        'dcintegration.discord' {
            description = "Gives access to the basic Discord Integration commands"
            setDefault('TRUE')
        }
        'dcintegration.admin' {
            description = "Gives access to the Discord Integration admin subcommands (like reload)"
            setDefault('OP')
        }
    }
    softDepend = ["Votifier", "dynmap", "floodgate-bukkit"]
}


repositories{
    maven {
        name = 'sonatype-oss'
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }

    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url 'https://jitpack.io' }
    maven { url "https://nexus.vankka.dev/repository/maven-public/" }
    maven { url "https://repository.dev.gotan.io/repository/gotan.os/" }
    maven { url "https://repo.opencollab.dev/maven-snapshots" }
    maven { url "https://repo.opencollab.dev/maven-releases" }
}
dependencies{

    // =========  Common  =====================
    embed 'de.erdbeerbaerlp:DiscordIntegration-Core:882716be'


    // ============= Spigot =============
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'


    //Votifier
    compileOnly('com.github.vexsoftware:votifier:v1.9'){
        transitive = false
    }

    //Dynmap-Bukkit for workaround
    compileOnly('com.github.webbukkit.dynmap:dynmap-api:v3.1'){
        transitive = false
    }

    //Floodgate
    compileOnly('org.geysermc.floodgate:api:2.0-SNAPSHOT')

    //Dynmap-API
    compileOnly('com.github.webbukkit:DynmapCoreAPI:2.5')
}
//Manifest attributes
jar {
    classifier = "1.17"
}

//Curseforge publishing
curseforge {
    if (project.hasProperty('curseforge.apikey')) { // $GRADLE_USER_HOME/gradle.properties
        apiKey = getProperty("curseforge.apikey")
        project {
            id = '440292'
            changelog = project.changelog
            releaseType = project.releaseType.toLowerCase()
            addGameVersion("1.17")
            mainArtifact(jar) {
                displayName = "Gradle-TestRelease"
            }
        }
        options{
            bukkitIntegration = true
            javaVersionAutoDetect = false
            javaIntegration = true
        }
    }
}